" " .M { Celda Muerta } 
"#" .V { Celda Viva }

[]
    "                              " +
    "                              " +
    "     ###                      " +
    "                              " +
    "                              " +
    "     #                        " +
    "      #                       " +
    "    ###                       " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
.tablero

[]
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
    "                              " +
.tableroB

@juego-de-la-vida

:swap-tableros
    $tableroB 
    $tablero 
    .tableroB 
    .tablero
    return


:juego-de-la-vida

    console.clear
    $tablero (imprimir-tablero) 
    100 sleep

    1 ..fila
    ::iterFila
        $tablero $$fila at ..tabFila

        1 ..columna
        ::iterCol
            $$fila $$columna (vecinosVivos)

            (:actualizar-muerta)
            (:actualizar-viva)

            drop

            $$columna 1 + ..columna
        $$columna $$tabFila len 1 - < @:iterCol
        $$fila 1 + ..fila
    $$fila $tablero len 1 - < @:iterFila
    
    (swap-tableros)

    @juego-de-la-vida

    ::set-muerta
        $tableroB $$fila at $$columna $M set
        return 

    ::set-viva
        $tableroB $$fila at $$columna $V set
        return 

    ::actualizar-viva
        $$tabFila $$columna at $M = return
        dup 2 < (:set-muerta)
        dup 3 > (:set-muerta)
        dup 2 = (:set-viva)
        dup 3 = (:set-viva)
        return

    ::actualizar-muerta
        $$tabFila $$columna at $V = return
        dup 3 = (:set-viva)
        dup 3 < (:set-muerta)
        dup 3 > (:set-muerta)
        return


:vecinosVivos
    {
        Entrada:
                * índiceFila
                * índiceColumna

        Salida:
                * númeroDeCeldasVivas
    }

    ..columna
    ..fila

    $$columna 1 - ..columna
    $$fila 1 - ..fila

    0 ..conteo

    $tablero dup $$fila 0 + at $$columna 0 + at $V = (:incrementar)
    $tablero dup $$fila 0 + at $$columna 1 + at $V = (:incrementar)
    $tablero dup $$fila 0 + at $$columna 2 + at $V = (:incrementar)

    $tablero dup $$fila 1 + at $$columna 0 + at $V = (:incrementar)
    $tablero dup $$fila 1 + at $$columna 2 + at $V = (:incrementar)

    $tablero dup $$fila 2 + at $$columna 0 + at $V = (:incrementar)
    $tablero dup $$fila 2 + at $$columna 1 + at $V = (:incrementar)
    $tablero dup $$fila 2 + at $$columna 2 + at $V = (:incrementar)

    $$conteo return

    ::incrementar
        $$conteo 1 + ..conteo
        return

:imprimir-tablero
    {
        Entrada:
                * tablero
    }

    ..tablero

    0 ..fila
    ::iter
        $$tablero $$fila at println
        $$fila 1 + ..fila
    $$fila $$tablero len < @:iter

    return

